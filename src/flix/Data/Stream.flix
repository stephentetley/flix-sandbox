/*
 * Copyright 2020 Stephen Tetley
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

use Data.Stream;
use Data.Stream.{Stream};
use Data.Step;
use Data.Step.{Done, Skip, Yield};

namespace Data {

    ///
    /// The Stream type.
    ///
    /// A stream provides "demand driven" access to its elements - successive
    /// elements are only produced when needed. Internally the stream is
    /// represented as a "pull stream" plus a finalizer.
    ///
    /// Note - a stream is not "replayable".
    ///
    pub opaque type Stream[a] = { generate: Unit ~> (Unit ~> Step[a]), finalize: Unit ~> Unit }

    ///
    /// The internal type of stream answers, not pub.
    /// `Skip` allows us to write `filter` and other functions that do not
    /// necessarily produce a value at each step.
    ///
    enum Step[a] {
        case Done,
        case Skip,
        case Yield(a)
    }

}

namespace Data/Stream {

    ///
    /// Returns the empty stream.
    ///
    pub def empty(): Stream[a] =
        let gen = () -> {
            let dummy = ref 0;
            let next = () -> {
                dummy := 1;
                Done
            };
            next
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Helper function to extract finalizer.
    ///
    def getFinalizer(s: Stream[a]): Unit -> Unit & Impure =
        let Stream(body) = s;
        body.finalize

    ///
    /// Helper function to extract generator.
    ///
    def getGenerator(s: Stream[a]): Unit -> (Unit ~> Step[a]) & Impure =
        let Stream(body) = s;
        body.generate

    ///
    /// Truncate a stream but keep its finalizer.
    ///
    def emptyOf(s: Stream[a]): Stream[a] =
        let gen = () -> {
            let dummy = ref 0;
            let next = () -> {
                dummy := 1;
                Done
            };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    ///
    /// Returns a one element stream containing `x`.
    ///
    pub def return(x: a): Stream[a] =
        let gen = () -> {
            let n = ref 0;
            let next = () -> {
                if (deref n >= 0)
                    Done
                else {
                    n := 1 + deref n;
                    Yield(x)
                }
            };
            next
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Returns the stream produced by applying the mapping function produced by Stream `sf` to the elements 
    /// produced by Stream `s`.
    ///    
    pub def ap(sf: Stream[a -> b], s: Stream[a]): Stream[b] = 
        zipWith((f,a) -> f(a), sf, s)


    pub def bind(s: Stream[a], k: a -> Stream[b]): Stream[b] =
        let gen = () -> {
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () ->
                match sf() {
                    case Yield(a) => { 
                        let generate2 = getGenerator(k(a));
                        let sf2 = generate2();
                        sf2()
                    }
                    case Skip     => Skip
                    case Done     => Done
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    pub def flatMap(k: a -> Stream[b], s: Stream[a]): Stream[b] = bind(s, k)

    ///
    /// Construct an Iterator.
    ///
    /// The Iterator must provide three functions.
    ///
    /// * `hasNext` to test whether the iterator can still produce more values.
    ///
    /// * `next` to produce another value iff `hasNext` has indicated more values are available.
    ///
    /// * `finalize` to be called when the iterator has exhausted all its input. `finalize` should close
    ///    any open resources.
    ///
    type alias Iterator[a] = { hasNext: Unit ~> Bool, next: Unit ~> a, finalize: Unit ~> Unit }

    ///
    /// Returns a stream generated by an Iterator.
    ///
    pub def fromIterator(iterator: Iterator[a]): Stream[a] =
        let hasNext = iterator.hasNext;
        let iterNext = iterator.next;
        let iterFinal = iterator.finalize;
        let gen = () -> {
            let next = () ->
                if (!hasNext()) 
                    Done 
                else 
                    Yield(iterNext());
            next as & Impure
        };
        Stream({ generate = gen, finalize = iterFinal })


    ///
    /// Returns a stream of all integers between `b` (inclusive) and `e` (exclusive).
    ///
    /// Returns an empty Stream if `b >= e`.
    ///
    pub def range(b: Int32, e: Int32): Stream[Int32] =
        if (b >= e)
            empty()
        else
            rangeHelper(b, e)

    ///
    /// Helper function for `range`.
    ///
    /// Precodition: b < e
    ///
    def rangeHelper(b: Int32, e: Int32): Stream[Int32] =
        let gen = () -> {
            let rx = ref b;
            let next = () -> {
                let x = deref rx;
                if (x >= e)
                    Done
                else {
                    rx := x + 1;
                    Yield(x)
                }
            };
            next
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Returns the stream `s` as a list.
    ///
    pub def toList(s: Stream[a]): List[a] & Impure =
        let generate1 = getGenerator(s);
        let finalize1 = getFinalizer(s);
        let sf = generate1();
        let ans = toListHelper(sf, Nil);
        finalize1();
        ans

    ///
    /// Helper function for `toList`.
    ///
    /// Precodition: b < e
    ///
    def toListHelper(sf : Unit ~> Step[a], ac: List[a]) : List[a] & Impure =
        match sf() {
            case Done     => List.reverse(ac)
            case Skip     => toListHelper(sf, ac)
            case Yield(a) => toListHelper(sf, a :: ac)
        }


    ///
    /// Applies `f` to every element of `s`.
    ///
    /// The function `f` must be impure.
    ///
    pub def foreach(f: a ~> Unit, s: Stream[a]): Unit & Impure =
        let generate1 = getGenerator(s);
        let finalize1 = getFinalizer(s);
        let sf = generate1();
        let ans = foreachHelper(f, sf);
        finalize1();
        ans

    ///
    /// Helper function for `foreach`.
    ///
    def foreachHelper(f: a ~> Unit, sf : Unit ~> Step[a]): Unit & Impure =
        match sf() {
            case Done => ()
            case Skip => foreachHelper(f, sf)
            case Yield(a) => {
                f(a);
                foreachHelper(f, sf)
            }
        }

    ///
    /// Build a stream by applying `f` to the seed value `st`.
    ///
    /// `f` should return `Some(a,st1)` to signal a new element `a` and a new seed value `st1`.
    ///
    /// `f` should return `None` to signal the end of building the stream.
    ///
    pub def unfold(f: b -> Option[(a,b)], st: b): Stream[a] =
        let gen = () -> {
            let state = ref st;
            let next = () ->
                match f(deref state) {
                    case None => Done
                    case Some(x,st2) => {
                        state := st2;
                        Yield(x)
                    }
                };
            next
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Build a stream by applying the function `next` to `()`. `next` is expected to encapsulate
    /// a stateful resource such as a file handle that can be iterated.
    ///
    /// `next` should return `Some(a)` to signal a new element `a`.
    ///
    /// `next` should return `None` to signal the end of building the stream.
    ///
    pub def unfoldWithIter(f: () ~> Option[a]): Stream[a] =
        let gen = () -> {
            let next = () ->
                match f() {
                    case None => Done
                    case Some(x) => Yield(x)
                };
            next as & Impure
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Returns the result of applying `f` to every element in Stream `s`.
    ///
    pub def map!(f: a -> b & e, s: Stream[a]): Stream[b] =
        let gen = () -> {
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () ->
                match sf() {
                    case Yield(a) => {
                        let a1 = f(a) as & Impure;
                        Yield(a1)
                    }
                    case Skip     => Skip
                    case Done     => Done
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    ///
    /// Returns the result of applying `f` to every element in Stream `s` and the accumulator `ac`.
    ///
    pub def mapAccum(f: (ac, a) -> (ac, b), ac: ac, s: Stream[a]): Stream[b] =
        let gen = () -> {
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () -> {
                let accum = ref ac;
                match sf() {
                    case Yield(a) => {
                        let (ac1, b) = f(deref accum, a);
                        accum := ac1;
                        Yield(b)
                    }
                    case Skip     => Skip
                    case Done     => Done
                }
            };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    ///
    /// Pair each element of stream `s` with its index, the index starts at `k`.
    ///
    pub def index(k: Int32, s: Stream[a]): Stream[(Int32, a)] =
        mapAccum((ix,a) -> (ix+1, (ix, a)), k, s)

    ///
    /// Pair each element of stream `s` with its index, the index starts at 0.
    ///
    pub def zeroIndex(s: Stream[a]): Stream[(Int32, a)] =
        mapAccum((ix,a) -> (ix+1, (ix, a)), 0, s)

    ///
    /// Returns a stream built from the elements of list `xs`.
    ///
    pub def fromList(xs: List[a]): Stream[a] =
        let gen = () -> {
            let rxs = ref xs;
            let next = () ->
                match (deref rxs) {
                    case Nil => Done
                    case x :: rs => {
                        rxs := rs;
                        Yield(x)
                    }
                };
            next
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Returns a stream built from the elements of array `arr`.
    ///
    pub def fromArray(arr: Array[a]): Stream[a] =
        let gen = () -> {
            let x = ref 0;
            let next = () -> {
                let ix = deref x;
                if (ix >= Array.length(arr))
                    Done
                else {
                    let p = Array.get(arr, ix);
                    x := ix + 1;
                    Yield(p)
                }
            };
            next
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Add element `x` to the front of stream `s`.
    ///
    pub def cons(x: a, s: Stream[a]) : Stream[a] =
        let gen = () -> {
            let generate1 = getGenerator(s);
            let sf = generate1();
            let initial = ref true;
            let next = () ->
                if (deref initial != true)
                    sf()
                else {
                    initial := false;
                    Yield(x)
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    ///
    /// Deconstruct the stream `s`.
    ///
    /// If the stream is productive return `Some(a,rest)` where `a` is the first element
    /// and `rest` is the rest of the stream.
    ///
    /// If the stream is exhausted return `None`.
    ///
    pub def view(s: Stream[a]): Option[(a, Stream[a])] & Impure =
        let generate1 = getGenerator(s);
        let finalize1 = getFinalizer(s);
        let sf = generate1();
        viewHelper(sf, finalize1)

    ///
    /// Helper function for `view`.
    ///
    def viewHelper(sf: Unit ~> Step[a], finalizer: Unit ~> Unit): Option[(a, Stream[a])] & Impure =
        match sf() {
            case Done => { finalizer(); None }
            case Skip => viewHelper(sf, finalizer)
            case Yield(a) => {
                let tail =
                    Stream({
                        generate = () ->
                            let next = () -> sf();
                            next as & Impure
                        ,
                        finalize = () -> finalizer()
                     });
                Some(a, tail)
            }
        }

    ///
    /// Returns a stream where the element at index `i` is `(a, b)` where
    /// `a` is the element at index `i` in `s1` and `b` is the element at index `i` in `s2`.
    ///
    /// If either `s1` or `s2` becomes depleted, then no further elements are added to the resulting stream.
    ///
    pub def zip(s1: Stream[a], s2: Stream[b]): Stream[(a,b)] = zipWith((a,b) -> (a,b), s1, s2)

    ///
    /// Returns a stream where the element at index `i` is `f(a, b)` where
    /// `a` is the element at index `i` in `s1` and `b` is the element at index `i` in `s2`.
    ///
    /// If either `s1` or `s2` becomes depleted, then no further elements are added to the resulting stream.
    ///
    pub def zipWith(f: (a,b) -> c, s1: Stream[a], s2: Stream[b]): Stream[c] =
        let gen = () -> {
            let generate1 = getGenerator(s1);
            let generate2 = getGenerator(s2);
            let sf1 = generate1();
            let sf2 = generate2();
            let next = () -> zipWithHelper(f, sf1, sf2);
            next as & Impure
        };
        let final = () -> {
            let finalize1 = getFinalizer(s1);
            let finalize2 = getFinalizer(s2);
            finalize1();
            finalize2()
        };
        Stream({ generate = gen, finalize = final })

    ///
    /// Helper function for `zipWith`.
    ///
    /// Any case with `Done` is `Done`.
    ///
    def zipWithHelper(f: (a,b) -> c, sf1: Unit ~> Step[a], sf2: Unit ~> Step[b]) : Step[c] & Impure =
        match (sf1(), sf2()) {
            case (Yield(a), Yield(b))   => Yield(f(a,b))
            case (Yield(a), Skip)       => zipWithHelperGoRight(f, a, sf2)
            case (Skip, Yield(b))       => zipWithHelperGoLeft(f, sf1, b)
            case (Skip, Skip)           => Skip
            case (_, _)                 => Done
        }

    ///
    /// Helper function for `zipWith`.
    ///
    /// Precondition: stream `s1` (left) has yielded the value `x` but `s2` (right) yielded a `Skip`.
    /// Go along s2 (right) to produce a value.
    ///
    def zipWithHelperGoRight(f: (a,b) -> c, x: a, sf: Unit ~> Step[b]) : Step[c] & Impure =
        match sf() {
            case Yield(y)   => Yield(f(x,y))
            case Done       => Done
            case Skip       => zipWithHelperGoRight(f, x, sf)
        }

    ///
    /// Helper function for `zipWith`.
    ///
    /// Precondition: stream `s2` (right) has yielded the value `x` but `s1` (left) yielded a `Skip`.
    /// Go along s2 (left) to produce a value.
    ///
    def zipWithHelperGoLeft(f: (a,b) -> c, sf: Unit ~> Step[a], y: b) : Step[c] & Impure =
        match sf() {
            case Yield(x)   => Yield(f(x,y))
            case Done       => Done
            case Skip       => zipWithHelperGoLeft(f, sf, y)
        }

    ///
    /// Applies `f` to a start value `ac` and all elements in `s` going from left to right.
    ///
    /// That is, the result is of the form: `f(...f(f(s, x1), x2)..., xn)`.
    ///
    pub def fold(f: (b, a) -> b & e, ac: b, s: Stream[a]): b & Impure =
        let generate1 = getGenerator(s);
        let finalize1 = getFinalizer(s);
        let sf = generate1();
        let ans = foldHelper(f, sf, ac);
        finalize1();
        ans


    ///
    /// Helper function for `fold`.
    ///
    def foldHelper(f: (b,a) -> b & e, sf : Unit ~> Step[a], ac: b): b & Impure =
        match sf() {
            case Done => ac
            case Skip => foldHelper(f, sf, ac)
            case Yield(a) =>
                foldHelper(f, sf, f(ac,a) as & Impure)
        }

    ///
    /// Returns `s2` appended to `s1`.
    ///
    pub def append(s1: Stream[a], s2: Stream[a]): Stream[a] =
        let gen = () -> {
            let generate1 = getGenerator(s1);
            let generate2 = getGenerator(s2);
            let sf1 = generate1();
            let sf2 = generate2();
            let from1 = ref true;
            let next = () ->
                if (deref from1 == false)
                    sf2()
                else
                    match sf1() {
                        case Skip => Skip
                        case Yield(a) => Yield(a)
                        case Done => {
                            from1 := false;
                            Skip
                        }
                    };
            next
        };
        let final = () -> { 
            let finalize1 = getFinalizer(s1);
            let finalize2 = getFinalizer(s2);
            finalize1();
            finalize2()
        };
        Stream({ generate = gen, finalize = final })

    ///
    /// Returns the first `n` elements of stream `s`.
    ///
    /// Returns `s` if `n > length(s)`.
    /// Returns the empty stream if `n < 0`.
    ///
    pub def take(n: Int32, s: Stream[a]): Stream[a] =
        if (n <= 0)
            emptyOf(s)
        else
            takeHelper(n, s)

    ///
    /// Helper function for `take`.
    ///
    /// Precondition: n > 0 .
    ///
    def takeHelper(n: Int32, s: Stream[a]): Stream[a] =
        let gen = () -> {
            let x = ref 0;
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () ->
                if (deref x >= n)
                    Done
                else {
                    match sf() {
                        case Yield(a) => {
                            x := deref x + 1;
                            Yield(a)
                        }
                        case Skip => Skip
                        case Done => Done
                    }
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })


    ///
    /// Returns the longest prefix of `s` that satisfies the predicate `f`.
    ///
    /// The function `f` must be pure.
    ///
    pub def takeWhile(f: a -> Bool, s: Stream[a]): Stream[a] =
        let gen = () -> {
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () ->
                match sf() {
                    case Yield(a) =>
                        if (f(a) == false)
                            Done
                        else
                            Yield(a)
                    case Skip => Skip
                    case Done => Done
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })


    ///
    /// Returns `s` without the first `n` elements.
    ///
    /// Returns the empty stream if `n > length(xs)`.
    /// Returns `xs` if `n < 0`.
    ///
    pub def drop(n: Int32, s: Stream[a]): Stream[a] =
        if (n <= 0)
            s
        else
            dropHelper(n, s)

    ///
    /// Helper function for `drop`.
    ///
    /// Precondition: n >= 0 .
    ///
    def dropHelper(n: Int32, s: Stream[a]): Stream[a] =
        let gen = () -> {
            let x = ref n;
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () -> match sf() {
                case Yield(a) =>
                    if (deref x == 0)
                        Yield(a)
                    else {
                        x := deref x - 1;
                        Skip
                    }
                case Skip => Skip
                case Done => Done
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    ///
    /// Returns `s` without the longest prefix that satisfies the predicate `f`.
    ///
    /// The function `f` must be pure.
    ///
    pub def dropWhile(f: a -> Bool, s: Stream[a]): Stream[a] =
        let gen = () -> {
            let afterPrefix = ref false;
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () ->
                match sf() {
                    case Yield(a) =>
                        if (deref afterPrefix == true)
                            Yield(a)
                        else
                            if (f(a) == true)
                                Skip
                            else {
                                afterPrefix := true;
                                Yield(a)
                            }
                    case Skip => Skip
                    case Done => Done
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    ///
    /// Returns a stream of every element in `s` that satisfies the predicate `f`.
    ///
    /// The function `f` must be pure.
    ///
    pub def filter(f: a -> Bool, s: Stream[a]): Stream[a] =
        let gen = () -> {
            let generate1 = getGenerator(s);
            let sf = generate1();
            let next = () ->
                match sf() {
                    case Done => Done
                    case Skip => Skip
                    case Yield(a) =>
                        if (f(a) == false)
                            Skip
                        else
                            Yield(a)
                };
                next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })

    ///
    /// Returns `true` if streams `s1` and `s1` have the same elements, i.e. are equivalent.
    ///
    /// Note - `sameElements` forces both streams, it is a so-called "terminal operation" and
    /// you will not be able to do anything else with the streams afterwards.
    ///
    pub def sameElements(s1: Stream[a], s2: Stream[a]): Bool & Impure =
        let generate1 = getGenerator(s1);
        let generate2 = getGenerator(s2);
        let finalize1 = getFinalizer(s1);
        let finalize2 = getFinalizer(s2);
        let sf1 = generate1();
        let sf2 = generate2();
        let ans = sameElementsHelper(sf1, sf2);
        finalize1();
        finalize2();
        ans


    ///
    /// Helper function for `sameElements`.
    ///
    def sameElementsHelper(sf1: Unit ~> Step[a], sf2: Unit ~> Step[a]): Bool & Impure =
        match (sf1(), sf2()) {
            case (Yield(a), Yield(b))   => if (a != b) false else sameElementsHelper(sf1, sf2)
            case (Yield(a), Skip)       => sameElementsHelperGoRight(a, sf1, sf2)
            case (Skip, Yield(b))       => sameElementsHelperGoLeft(sf1, b, sf2)
            case (Yield(_), Done)       => false
            case (Done, Yield(_))       => false
            case (Skip, Skip)           => sameElementsHelper(sf1, sf2)
            case (Skip, Done)           => sameElementsHelper(sf1, sf2)
            case (Done, Skip)           => sameElementsHelper(sf1, sf2)
            case (Done, Done)           => true
        }

    ///
    /// Helper function for `sameElements`.
    ///
    /// Precondition: stream `sf1` (left) has yielded the value `x` but `sf2` (right) yielded a `Skip`.
    /// Go along sf2 (right) to produce a value, if same continue along both.
    ///
    def sameElementsHelperGoRight(x: a, sf1: Unit ~> Step[a], sf2: Unit ~> Step[a]) : Bool & Impure =
        match sf2() {
            case Yield(y)   => if (x != y) false else sameElementsHelper(sf1, sf2)
            case Done       => false
            case Skip       => sameElementsHelperGoRight(x, sf1, sf2)
        }

    ///
    /// Helper function for `sameElements`.
    ///
    /// Precondition: stream `sf2` (right) has yielded the value `x` but `sf1` (left) yielded a `Skip`.
    /// Go along sf2 (left) to produce a value, if same continue along both.
    ///
    def sameElementsHelperGoLeft(sf1: Unit ~> Step[a], y: a, sf2: Unit ~> Step[a]) : Bool & Impure =
        match sf1() {
            case Yield(x)   => if (x != y) false else sameElementsHelper(sf1, sf2)
            case Done       => true
            case Skip       => sameElementsHelperGoLeft(sf1, y, sf2)
        }

    ///
    /// Returns the substream of `xs` from index `b` (inclusive) to index `e` (exclusive).
    ///
    /// That is, an element at index `i` in `xs` is part of the returned sublist if and only if `i >= b` and `i < e`.
    /// Note: Indices that are out of bounds in `xs` are not considered (i.e. slice(b, e, xs) = slice(max(0,b), min(length(xs),e), xs)).
    ///
    pub def slice(b: Int32, e: Int32, s: Stream[a]): Stream[a] =
        if (b >= e)
            emptyOf(s)
        else
            sliceHelper(b, e, s)

    ///
    /// Helper function for `slice`.
    ///
    /// Preconditions: b >= 0 && b < e
    ///
    pub def sliceHelper(b: Int32, e: Int32, s: Stream[a]): Stream[a] =
        let gen = () -> {
            let generate1 = getGenerator(s);
            let sf = generate1();
            let ix = ref 0;
            let next = () ->
                match sf() {
                    case Done => Done
                    case Skip => Skip
                    case Yield(a) => {
                        let i = deref ix;
                        if (i < b || i >= e) {
                            ix := i + 1;
                            Skip
                        } else {
                            ix := i + 1;
                            Yield(a)
                        }
                    }
                };
            next
        };
        Stream({ generate = gen, finalize = getFinalizer(s) })


    ///
    /// Returns a stream with the element `a` repeated `n` times.
    ///
    /// Returns the empty stream if `n < 0`.
    ///
    pub def repeat(a: a, n: Int32): Stream[a] =
        if (n <= 0)
            empty()
        else repeatHelper(a, n)

    ///
    /// Helper function for `repeat`.
    ///
    def repeatHelper(a: a, n: Int32): Stream[a] =
        let gen = () -> {
            let ix = ref 0;
            let next = () ->
                if (deref ix >= n)
                    Done
                else {
                    ix := deref ix + 1;
                    Yield(a)
                };
            next
        };
        Stream({ generate = gen, finalize = _ -> () as & Impure })

    ///
    /// Applies `f` to a start value `ac` and all `Ok` elements in `s` going from left to right.
    /// If an `Err` is encountered the stream closed and the error is returned.
    ///
    pub def foldResult(f: (b, a) -> Result[b, err] & e, ac: b, s: Stream[a]): Result[b, err] & Impure =
        let generate1 = getGenerator(s);
        let finalize1 = getGenerator(s);
        let sf = generate1();
        let ans = foldResultHelper(f, sf, ac);
        finalize1();
        ans

    ///
    /// Helper function for `foldResult`.
    ///
    def foldResultHelper(f: (b,a) -> Result[b, err] & e, sf: Unit ~> Step[a], ac: b): Result[b, err] & Impure =
        match sf() {
            case Done => Ok(ac)
            case Skip => foldResultHelper(f, sf, ac)
            case Yield(a) => match f(ac, a) {
                case Err(err) => Err(err)
                case Ok(ac1) => foldResultHelper(f, sf, ac1)
            } as & Impure
        }

}